grammar fr.mleduc.ExplicitOperations with org.eclipse.xtext.common.Terminals

generate explicitOperations "http://www.mleduc.fr/ExplicitOperations"

Model:
	(languages+=Language | operations+=Operation | artifacts+=Artifact | composition+=Composition)*;

/**
 * External to the language CS
 */
Operation:
	'operation' name=ID
	('inputs'
	inputs+=OperationParameter*)?
	('outputs'
	outputs+=OperationParameter*)?;

OperationParameter:
	name=ID ':' type=[Language|ID];

Language:
	'language' name=ID
	dependencies+=LanguageDependency*;

/**
 * Internal to the language CS
 */
LanguageDependency:
	name=ID ':' type=[Language|ID];


Artifact:
	'artifact' name=ID ':' type=[Language|ID]
;

Applicable:
	OperationParameter | LanguageDependency
;

Variable:
	name=ID
;

Composition:
	'composition' operation=[Applicable|ID] '[' parameters+=[Artifact|ID]* ']' '->' '[' result+=Variable ']'	
;