grammar fr.mleduc.ExplicitOperations with org.eclipse.xtext.common.Terminals

generate explicitOperations "http://www.mleduc.fr/ExplicitOperations"

Model:
	'package' name=ID
	(languages+=Language | operations+=Operation | artifacts+=Artifact | compositions+=Composition |
	featureModels+=FeatureModel)*;

FeatureModel:
	name=ID feature+=Feature;

Feature:
	AtomicFeature | OneOf | SomeOf;

AtomicFeature returns Feature:
	{AtomicFeture}
	name=ID;

OneOf returns Feature:
	{OneOf}
	name=ID '[' children+=Feature ']';

SomeOf returns Feature:
	{SomeOf}
	name=ID;

	/**
 * External to the language CS
 */
Operation:
	'operation' name=ID
	'inputs'
	inputs+=OperationParameter*
	'output'
	output=OperationParameter;

OperationParameter:
	name=ID ':' type=[Language|ID] cardinality=Cardinality;

Cardinality:
	'[' start=CardVal '..' stop=CardVal ']';

CardVal:
	{CardVal0} '0' |
	{CardVal1} '1' |
	{CardValN} '*';

Language:
	'language' name=ID
	dependencies+=LanguageDependency*;

	/**
 * Internal to the language CS
 */
LanguageDependency:
	name=ID ':' type=[Language|ID];

Artifact:
	'artifact' name=ID ':' type=[Language|ID] ('[' parameters+=ArtifactParameter* ']')?;

ArtifactParameter:
	name=[LanguageDependency|ID] '=>' artifact=[Referentiable|ID];

Referentiable:
	Artifact | Composition;

Composition:
	'composition' name=ID '::' operation=[Operation|ID] '[' parameters+=CompositionParameter* ']';

CompositionParameter:
	name=[OperationParameter|ID] '=>' artifact=[Referentiable|ID];