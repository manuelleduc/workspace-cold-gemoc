grammar fr.mleduc.ExplicitOperations with org.eclipse.xtext.common.Terminals

generate explicitOperations "http://www.mleduc.fr/ExplicitOperations"

Model:
	(languages+=Language | operations+=Operation | artifacts+=Artifact | compositions+=Composition)*;

	/**
 * External to the language CS
 */
Operation:
	'operation' name=ID
	'inputs'
	inputs+=OperationParameter*
	'output'
	output=OperationParameter;

OperationParameter:
	name=ID ':' type=[Language|ID] cardinality=Cardinality;

Cardinality:
	'[' start=CardVal '..' stop=CardVal ']';

CardVal:
	{CardVal0} '0' |
	{CardVal1} '1' |
	{CardValN} '*';

Language:
	'language' name=ID
	dependencies+=LanguageDependency*;

	/**
 * Internal to the language CS
 */
LanguageDependency:
	name=ID ':' type=[Language|ID];

Artifact:
	'artifact' name=ID ':' type=[Language|ID];
	
Referentiable:
	Artifact | Composition
;

Composition:
	'composition' name=ID '::' operation=[Operation|ID] '[' parameters+=CompositionParameter* ']';

CompositionParameter:
	name=[OperationParameter|ID] '=>' artifact=[Referentiable|ID];